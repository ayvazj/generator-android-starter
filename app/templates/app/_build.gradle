buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.stanfy.spoon:spoon-gradle-plugin:1.0.3'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.4'
    }
}

repositories {
    jcenter()
}

/*
 * App compilation configuration
 */

apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    defaultConfig {
        minSdkVersion <%= androidMinSdkVersion %>
        targetSdkVersion <%= androidTargetSdkVersion %>
        multiDexEnabled true

        testInstrumentationRunner "<%= appPackage %>.TestTestRunner"
    }
    productFlavors {
        env_test {
            applicationId '<%= appPackage %>.test'
            versionCode 1
            versionName "1.0.0"
        }
        env_prod {
            applicationId '<%= appPackage %>'
            versionCode 1
            versionName "1.0.0"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }

    lintOptions {
        disable 'InvalidPackage'
    }
}

apply plugin: 'idea'
idea {
    module {
        testOutputDir = file('build/test-classes/debug')
    }
}

apply plugin: 'jacoco'
jacoco {
    toolVersion = '0.7.2.201409121644'
}

def coverageSourceDirs = [
        'src/main/java'
]

task jacocoTestReport(type: JacocoReport, dependsOn: 'testEnv_prodDebug') {
    group = "Reporting"

    description = "Generate Jacoco coverage reports"

    classDirectories = fileTree(
            dir: 'build/intermediates/classes/env_prod/debug',
            excludes: ['**/R.class',
                       '**/R$*.class',
                       '**/*$ViewInjector*.*', // Ignore ButterKnife view injectors
                       '**/*$InjectAdapter*',  // Ingore Dagger inject adapters
                       '**/*$ModuleAdapter*',  // Ingore Dagger module adapters
                       '**/BuildConfig.*',
                       '**/Manifest*.*']
    )
    sourceDirectories = files(coverageSourceDirs)
    executionData = files('build/jacoco/testEnv_prodDebug.exec')

    reports {
        xml.enabled = true
        html.enabled = true
    }
}

/*
 * App dependencies
 */

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:23.0.+'
    compile 'com.android.support:cardview-v7:23.0.+'
    compile 'com.android.support:recyclerview-v7:23.0.+'
    compile 'com.android.support:support-v4:23.0.+'
    compile 'com.android.support:support-annotations:23.0.+'
    compile 'com.cocosw:bottomsheet:1.+@aar'
    compile 'joda-time:joda-time:2.4'
    compile 'org.joda:joda-money:0.9.1'
    compile 'org.joda:joda-convert:1.7'
    compile 'com.atomicleopard:expressive:0.9.5'
    compile 'com.google.guava:guava:18.0'
    compile 'com.google.code.gson:gson:2.3'
    compile 'org.apache.commons:commons-lang3:3.3.2'
    compile 'commons-io:commons-io:2.4'
    compile 'com.squareup:otto:1.3.5'
    compile 'com.squareup.okhttp:okhttp:2.2.0'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.2.0'
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    provided 'javax.annotation:jsr250-api:1.0'
    compile 'com.google.dagger:dagger:2.0.1'
    apt 'com.google.dagger:dagger-compiler:2.0.1'
    compile 'com.squareup.flow:flow:0.12'
    compile 'com.squareup.flow:flow-path:0.12'
    compile 'com.squareup.mortar:mortar:0.19'
    compile 'com.jakewharton.timber:timber:3.1.0'
    compile 'org.hibernate:hibernate-validator:4.3.2.Final'
    compile 'com.mixpanel.android:mixpanel-android:4.3.1@aar'
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'io.reactivex:rxandroid:1.0.1'
    compile 'io.reactivex:rxjava:1.0.14'

    androidTestCompile('junit:junit:4.12') {
        exclude module: 'javawriter'
    }
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.1') {
        exclude module: 'javawriter'
    }
//    // TODO https://code.google.com/p/android-test-kit/issues/detail?id=157
//    //androidTestCompile 'com.android.support.test.espresso:espresso-contrib:2.2'
    androidTestCompile('com.android.support.test:runner:0.4') {
        exclude module: 'javawriter'
    }
    androidTestCompile('com.android.support.test:rules:0.4') {
        exclude module: 'javawriter'
    }

    androidTestCompile('com.squareup.spoon:spoon-client:1.1.10') {
        exclude module: 'javawriter'
    }
    androidTestCompile('com.squareup.retrofit:retrofit-mock:1.9.0')

    androidTestCompile('org.mockito:mockito-core:1.10.19')
}

apply plugin: 'spoon'
spoon {
    debug = true
    noAnimations = true

    if (project.hasProperty('spoonClassName')) {
        className = project.spoonClassName

        if (project.hasProperty('spoonMethodName')) {
            methodName = project.spoonMethodName
        }
    }

    if (project.hasProperty('spoonTestSize')) {
        testSize = project.spoonTestSize
    }
}
